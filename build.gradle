import io.gitlab.arturbosch.detekt.Detekt
import io.gitlab.arturbosch.detekt.report.ReportMergeTask

buildscript {
    ext {
        compose_version = '1.2.0'
        room_version = '2.4.3'
    }
}// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '7.3.0' apply false
    id 'com.android.library' version '7.3.0' apply false
    id 'org.jetbrains.kotlin.android' version '1.7.0' apply false
    id 'io.gitlab.arturbosch.detekt' version '1.21.0' apply false
}

task detektMergeReport(type: ReportMergeTask) {
    output = project.layout.buildDirectory.file('reports/detekt/merged.sarif')
}

allprojects {
    apply {
        plugin 'io.gitlab.arturbosch.detekt'
    }
    dependencies {
        detektPlugins 'io.gitlab.arturbosch.detekt:detekt-formatting:1.21.0'
    }

    detekt {
        config = files("${rootProject.rootDir}/detekt.yml")
        buildUponDefaultConfig true

        tasks.withType(Detekt) { d ->
            finalizedBy(detektMergeReport)
            detektMergeReport.configure { m ->
                m.input.from(d.sarifReportFile)
            }
        }
    }
}

task connectedAndroidTestCollectReports(type: Copy) {
    subprojects.forEach { p ->
        with copySpec {
            from p.layout.buildDirectory.file('outputs/androidTest-results/connected/test-result.pb')
            rename "test-result.pb", "${p.name}-test-result.pb"
        }
    }
    into rootProject.layout.buildDirectory.dir('outputs/androidTest')
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
